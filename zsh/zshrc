autoload -Uz compinit promptinit colors
colors
compinit
promptinit

MY_CONF_DIR=~/.zshconf

#menu completion
zstyle ':completion:*' menu select
#make aliasc completion work naturally
setopt completealiases

#make history search depend on current input
[[ -n "${key[PageUp]}"   ]]  && bindkey  "${key[PageUp]}"    history-beginning-search-backward
[[ -n "${key[PageDown]}" ]]  && bindkey  "${key[PageDown]}"  history-beginning-search-forward
HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.zshist

#vim binding
bindkey -v

# Custom prompt.

#Environment variable for the status of VIM input mode.
MY_VIM_PROMPT="[NO]"

#Version control info for git.
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
setopt prompt_subst
zstyle ':vcs_info:git:*' formats '%b'

#Widgets updating the environment variable once the VIM mode changes.
function zle-line-init zle-keymap-select
{
    if [[ $KEYMAP == "viins" || $KEYMAP == "main" ]]; then
        MY_VIM_PROMPT="[IN]"
    elif [[ $KEYMAP == "vicmd" ]]; then
        MY_VIM_PROMPT="[NO]"
    fi
    zle reset-prompt;
}
zle -N zle-keymap-select
zle -N zle-line-init

#The actual prompt variables
PROMPT="%{%B%}\$MY_VIM_PROMPT%{%b%} %B%F{red}%n%f@%F{blue}%m%f%b%F{yellow}(%~)%f%# "
RPROMPT="%{%F{green}%}\$vcs_info_msg_0_%{%f%}%(?..%{$fg_bold[red]%}[%?]%{$reset_color%b%})"

setopt correct # spelling correction for commands
setopt correctall # spelling correction for arguments

#enable autojump
if [ $commands[autojump] ]; then
    source /usr/share/autojump/autojump.zsh
fi

# Alias definitions
source $MY_CONF_DIR/aliases
